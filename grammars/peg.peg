# Hierarchical syntax
Grammar    <- Spacing_ Definition+ EndOfFile_
Definition <- Identifier LEFTARROW Expression

Expression <- Sequence (SLASH Sequence)*
Sequence   <- Prefix*
Prefix     <- (AND / NOT)? Suffix
Suffix     <- Primary (QUESTION / STAR / PLUS)?
Primary    <- Identifier !LEFTARROW
            / OPEN Expression CLOSE
            / Literal / Class / DOT

# Lexical syntax
Identifier <- IdentStart IdentCont* Spacing_
IdentStart <- [a-zA-Z_]
IdentCont  <- IdentStart / [0-9]

Literal    <- ['] (!['] Char)* ['] Spacing_
            / ["] (!["] Char)* ["] Spacing_
Class      <- '[' (!']' Range)* ']' Spacing_
Range      <- Char '-' Char / Char
Char       <- '\\' [nrt'"\[\]\\]
            / '\\' [0-2][0-7][0-7]
            / '\\' [0-7][0-7]?
            / !'\\' .

LEFTARROW  <- '<-' Spacing_
SLASH      <- '/' Spacing_
AND        <- '&' Spacing_
NOT        <- '!' Spacing_
QUESTION   <- '?' Spacing_
STAR       <- '*' Spacing_
PLUS       <- '+' Spacing_
OPEN       <- '(' Spacing_
CLOSE      <- ')' Spacing_
DOT        <- '.' Spacing_

Spacing_   <- (Space_ / Comment_)*
Comment_   <- '#' (!EndOfLine_ .)* EndOfLine_
Space_     <- ' ' / '\t' / EndOfLine_
EndOfLine_ <- '\r\n' / '\n' / '\r'
EndOfFile_ <- !.
