Pattern    <- Spacing_ (Expression / Grammar) EndOfFile_
Grammar    <- Definition+
Definition <- Identifier '<-' Expression

Expression <- Sequence ('/' Sequence)*
Sequence   <- Prefix*
Prefix     <- (AND / NOT)? Suffix
Suffix     <- Primary (QUESTION / STAR / PLUS)?
Primary    <- Identifier !'<-'
            / '(' Expression ')'
            / Literal / Class
            / '{' Expression '}'
            / '{+' Expression '+}'
            / DOT

# Lexical syntax
Identifier <- IdentStart IdentCont* Spacing_
IdentStart <- [a-zA-Z_]
IdentCont  <- IdentStart / [0-9]

Literal    <- ['] (!['] Char)* ['] Spacing_
            / ["] (!["] Char)* ["] Spacing_
Class      <- '[' CARAT? (!']' Range)* ']' Spacing_
Range      <- Char '-' Char / Char
Char       <- '\\' [nrt'"\[\]\\]
            / '\\' [0-2][0-7][0-7]
            / '\\' [0-7][0-7]?
            / !'\\' .

AND        <- '&' Spacing_
NOT        <- '!' Spacing_
QUESTION   <- '?' Spacing_
STAR       <- '*' Spacing_
PLUS       <- '+' Spacing_
DOT        <- '.' Spacing_
CARAT      <- '^' Spacing_

Spacing_   <- (Space_ / Comment_)*
Comment_   <- '#' (!EndOfLine_ .)* EndOfLine_
Space_     <- ' ' / '\t' / EndOfLine_
EndOfLine_ <- '\r\n' / '\n' / '\r'
EndOfFile_ <- !.
