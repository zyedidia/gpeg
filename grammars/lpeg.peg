pattern         <- exp !.
exp             <- S (alternative / grammar)

alternative     <- seq ('/' S seq)*
seq             <- prefix*
prefix          <- '&' S prefix / '!' S prefix / suffix
suffix          <- primary S (([+*?]
                            / '^' [+\-]? num
                            / '->' S (string / '{}' / name)
                            / '=>' S name) S)*

primary         <- '(' exp ')' / string / class / defined
                 / '{:' (name ':')? exp ':}'
                 / '=' name
                 / '{*' exp '*}' # bare capture
                 / '{~' exp '~}' # substitution capture
                 / '{|' exp '|}' # table capture
                 / '{+' exp '+}' # memoization expression
                 / '{' exp '}'   # string capture
                 / '.'
                 / name S !arrow

grammar         <- definition+
definition      <- name S arrow exp

class           <- '[' '^'? item (!']' item)* ']'
item            <- defined / range / .
range           <- . '-' (!']' .)

S               <- (space / comment)*   # spaces and comments
name            <- [A-Za-z][A-Za-z0-9_]*
arrow           <- '<-'
num             <- [0-9]+
string          <- '"' (!'"' .)* '"' / "'" (!"'" .)* "'"
defined         <- '%' name

comment         <- '#' (!eol .)* eol
space           <- ' ' / '\t' / eol
eol             <- '\r\n' / '\n' / '\r'
